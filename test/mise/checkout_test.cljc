(ns mise.checkout-test
  (:require [clojure.test :refer [deftest is testing]]
            [mise.checkout :as checkout]
            [mise.cart :as cart]
            [mise.pricing :as pricing]))

(def c (-> (cart/cart)
           (cart/add-line {:sku "ph-m" :qty 1 :unit-price (pricing/price 38000)})))

(deftest stage-order-test
  (is (= :contact (:stage (checkout/checkout c))))
  (is (= [:contact :shipping :payment :review :confirmed] checkout/stages)))

(deftest validate-stage-test
  (let [co (checkout/checkout c)]
    (is (seq (checkout/validate-stage co :contact))) ; missing email+name
    (is (nil? (checkout/validate-stage
               (-> co (checkout/set-field :contact :email "a@b") (checkout/set-field :contact :name "x"))
               :contact)))))

(deftest next-stage-guards-validation-test
  (let [co (checkout/checkout c)]
    (is (:errors (checkout/next-stage co)))              ; contact incomplete
    (let [filled (-> co
                     (checkout/set-field :contact :email "a@b")
                     (checkout/set-field :contact :name "x"))
          r (checkout/next-stage filled)]
      (is (:ok r))
      (is (= :shipping (:stage (:ok r)))))))

(defn- advance [co]
  (:ok (checkout/next-stage co)))

(defn filled-checkout
  "A checkout with all stage fields filled, advanced to :review."
  []
  (let [co (-> (checkout/checkout c)
               (checkout/set-field :contact :email "a@b")
               (checkout/set-field :contact :name "x")
               (checkout/set-field :shipping :address "1-2-3")
               (checkout/set-field :shipping :city "Tokyo")
               (checkout/set-field :shipping :postal "100-0001")
               (checkout/set-field :shipping :country "JP")
               (checkout/set-field :payment :payment-method-ref "card"))]
    (-> co advance advance advance))) ; contact->shipping->payment->review

(deftest place-order-test
  (let [co (filled-checkout)
        port (checkout/mock-payment-port)
        r (checkout/place-order co port {})
        ord (:order r)]
    (is (:ok r))
    (is ord)
    (is (= :pending (:status ord)))
    (is (= :authorized (get-in ord [:payment-ref :status])))
    (is (= 38000 (get-in ord [:totals :total :amount])))))

(deftest place-order-rejects-non-review-test
  (let [co (checkout/checkout c)
        r (checkout/place-order co (checkout/mock-payment-port) {})]
    (is (:error r))))

(deftest mock-payment-port-test
  (let [port (checkout/mock-payment-port)
        ref (checkout/authorize port {:order 1} (pricing/price 100))]
    (is (= :authorized (:status ref)))
    (is (= :captured (:status (checkout/capture port {} ref))))))
