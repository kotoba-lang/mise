(ns mise.cart-test
  (:require [clojure.test :refer [deftest is testing]]
            [mise.cart :as cart]
            [mise.pricing :as pricing]))

(def line {:sku "ph-m" :name "パーカ M" :qty 1 :unit-price (pricing/price 38000)})

(deftest add-line-dedupes-and-merges-test
  (let [c (-> (cart/cart) (cart/add-line line) (cart/add-line (assoc line :qty 2)))]
    (is (= 1 (cart/line-count c)))
    (is (= 3 (:qty (first (:lines c)))))
    (is (= 3 (cart/item-count c)))))

(deftest update-qty-test
  (let [c (-> (cart/cart) (cart/add-line line))]
    (is (= 5 (:qty (first (:lines (cart/update-qty c "ph-m" 5))))))
    (testing "qty <= 0 removes the line"
      (is (cart/empty? (cart/update-qty c "ph-m" 0))))))

(deftest remove-line-test
  (let [c (-> (cart/cart) (cart/add-line line) (cart/remove-line "ph-m"))]
    (is (cart/empty? c))))

(deftest totals-test
  (let [c (-> (cart/cart)
              (cart/add-line line)
              (cart/add-line {:sku "x" :qty 2 :unit-price (pricing/price 1000)}))]
    (is (= 40000 (:amount (:total (cart/totals c)))))))
